-- @atlcompiler atl2010
-- @path statechart=/CapiTrain/EMF/statechart.ecore
-- @path petrinet=/CapiTrain/EMF/petrinet.ecore

module Petrinet2Statechart;

create pnetOut: petrinet refining pnetInput: petrinet;

-- drop the places
rule Petrinet2PetrinetAndRuleNext {
	from
		p: petrinet!Place  (
			thisModule.getPlaceNamesToDrop().includes(p.name)
		)
	to
		drop
} 

helper def: getPlaceNamesToDrop(): Sequence(String) =
	if not thisModule.getPlacesInAnd().oclIsUndefined()
	then thisModule.getPlacesInAnd() -> select(p | not (p.name = petrinet!Transition -> allInstances() -> select(t | 
				t.name = thisModule.getNextAndNames().first()).first().postp.first().name))
		-> collect(p | p.name)
	else OclUndefined
	endif
	;

helper def: getPlacesInAnd(): Sequence(petrinet!Place) =
	petrinet!Place -> allInstances() 
		-> select(p | p.pret.size() = 1)
		-> select(p | p.pret.first().name = thisModule.getNextAndNames().first())
	;

helper def: getNextAndNames(): Sequence(String) =
	petrinet!Transition -> allInstances() -> select(t | t.isPostpAndTransition()) 
	-> collect(t | t.name);

helper context petrinet!Transition def: isPostpAndTransition(): Boolean = 
	if self.postp.size() > 1
	then (thisModule.posttEquivalents(self.name, self.postp) and thisModule.pretEquivalents(self.name, self.postp))
		or (thisModule.nextEmpty(self.postp) and thisModule.pretEquivalents(self.name, self.postp))
	else false
	endif
;
	
helper def: nextEmpty(postp: Sequence(petrinet!Place)): Boolean =
	postp -> iterate(p; ret: Boolean = true |
		ret and p.postt.size() = 0
	);
	
helper def: posttEquivalents(name: String, postp: Sequence(petrinet!Place)): Boolean = 
	postp -> iterate(p; ret: Boolean = true |
		if p.postt.size() = 1
		then ret and (postp.first().postt.first().name = p.postt.first().name)
		else ret and false
		endif	
	);
	
helper def: pretEquivalents(name: String, postp: Sequence(petrinet!Place)): Boolean = 
	postp -> iterate(p; ret: Boolean = true |
		if (postp.first().pret.size() = 1 and p.pret.size() = 1)
		then ret and (postp.first().pret.first().name = p.pret.first().name)
		else ret and false
		endif	
	);